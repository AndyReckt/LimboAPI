import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'checkstyle'
}

apply plugin: 'org.cadixdev.licenser'
apply from: '../gradle/checkstyle.gradle'
apply plugin: 'com.github.johnrengelman.shadow'

license {
    matching(includes: ['**/limbofilter/config/helpers/**']) {
        header = project.rootProject.file('HEADER_BUNGEECORD.txt')
    }
    header = project.rootProject.file('HEADER.txt')
}

dependencies {
    compileOnly project(':limboapi-api')
    compileOnly project(':limboapi-plugin')
    compileOnly files('../libs/3.0.0.jar')

    annotationProcessor 'com.velocitypowered:velocity-api:3.0.0-SNAPSHOT'

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.1.2'
}

task processSources(type: Sync) {
    from sourceSets.main.java.srcDirs
    inputs.property 'version', version
    filter ReplaceTokens, tokens: [version: version]
    into "$buildDir/src"
}

tasks.withType(Checkstyle) {
    exclude('**/net/elytrium/limbofilter/config/Settings.java')
    exclude('**/net/elytrium/limbofilter/generator/CaptchaPainter.java') // Parameter Name
    exclude('**/net/elytrium/limbofilter/handler/FallingCheckHandler.java') // Member Name
}

artifacts {
    archives shadowJar
}

compileJava.source = processSources.outputs
